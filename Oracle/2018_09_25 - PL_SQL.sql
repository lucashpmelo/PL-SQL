DECLARE
  CURSOR CUR_NOTA IS SELECT * FROM TB_NOTA WHERE VL_DESCONTO > 0;
  CURSOR CUR_NOTA_ITEM(PCOD_NOTA TB_NOTA.COD_NOTA%TYPE) IS 
      SELECT COD_NOTA_ITEM, QT_QUANTIDADE, VL_UNITARIO, VL_DESCONTO FROM TB_NOTA_ITEM 
             WHERE COD_NOTA = PCOD_NOTA FOR UPDATE;
  WNOTA_ITEM CUR_NOTA_ITEM%ROWTYPE; 
  WINDICE NUMBER;
  WTOTAL NUMBER;
  WDESCONTO TB_NOTA_ITEM.VL_DESCONTO%TYPE;
  WDIFERENCA TB_NOTA_ITEM.VL_DESCONTO%TYPE;
  WCOD_ITEM TB_NOTA_ITEM.COD_NOTA_ITEM%TYPE; 
BEGIN
  FOR WI IN CUR_NOTA LOOP
      SELECT SUM(QT_QUANTIDADE * VL_UNITARIO) INTO WTOTAL FROM TB_NOTA_ITEM 
             WHERE COD_NOTA=WI.COD_NOTA; 
      WINDICE := WI.VL_DESCONTO / WTOTAL; 
      OPEN CUR_NOTA_ITEM(WI.COD_NOTA);
      LOOP
         FETCH CUR_NOTA_ITEM INTO WNOTA_ITEM;
         EXIT WHEN CUR_NOTA_ITEM%NOTFOUND;
         WDESCONTO:= WNOTA_ITEM.QT_QUANTIDADE * WNOTA_ITEM.VL_UNITARIO * WINDICE;
         UPDATE TB_NOTA_ITEM SET VL_DESCONTO = WDESCONTO WHERE CURRENT OF CUR_NOTA_ITEM;
         WCOD_ITEM := WNOTA_ITEM.COD_NOTA_ITEM;
       END LOOP;
       SELECT SUM(VL_DESCONTO) INTO WDESCONTO FROM TB_NOTA_ITEM WHERE COD_NOTA=WI.COD_NOTA;
       IF WDESCONTO <> WI.VL_DESCONTO THEN
          WDIFERENCA := WI.VL_DESCONTO - WDESCONTO;
          UPDATE TB_NOTA_ITEM SET VL_DESCONTO = VL_DESCONTO + WDIFERENCA WHERE COD_NOTA_ITEM=WCOD_ITEM; 
       END IF;
       CLOSE CUR_NOTA_ITEM;
  END LOOP;
END;